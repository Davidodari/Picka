
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.0'
group 'com.davidodari.picka'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/picka-release.aar")
            groupId this.group
            artifactId 'picka'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('username') ? username: System.getenv('username')
    key = project.hasProperty('apikey') ? apikey: System.getenv('apikey')
    publications = ['Production']
    configurations = ['archives']
    dryRun = false
    override = false
    publish = true
    pkg {
        repo = 'Picka'
        name = 'com.davidodari.picka'
        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}
